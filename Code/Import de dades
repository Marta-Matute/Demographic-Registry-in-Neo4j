// Eliminem els possibles nodes que puguin existir previament
MATCH (n)
DETACH DELETE n;

// Importem les dades de les persones 
LOAD CSV WITH HEADERS FROM 'https://docs.google.com/spreadsheets/d/e/2PACX-1vTfU6oJBZhmhzzkV_0-avABPzHTdXy8851ySDbn2gq32WwaNmYxfiBtCGJGOZsMgCWjzlEGX4Zh1wqe/pub?output=csv' AS row 
CREATE (p:PERSONA {Id: toInteger(row.Id), Any: toInteger(row.Year), Nom: row.name, Cognom: row.surname, Segon_Cognom: row.second_surname});

// Importem les dades de l'habitatge
LOAD CSV WITH HEADERS FROM 'https://docs.google.com/spreadsheets/d/e/2PACX-1vT0ZhR6BSO_M72JEmxXKs6GLuOwxm_Oy-0UruLJeX8_R04KAcICuvrwn2OENQhtuvddU5RSJSclHRJf/pub?output=csv' AS row
CREATE (:HABITATGE {Municipi: row.Municipi, Id_Llar: toInteger(row.Id_Llar), Any_Padro: toInteger(row.Any_Padro), Carrer: row.Carrer, Numero: toInteger(row.Numero)});

// Esborrem tots els indexs que puguin existir
CALL apoc.schema.assert({},{},true) YIELD label, key
RETURN *; 

// Creem indexos per a crear les relacions de manera mes rapida
create index for (p:PERSONA) on (p.Id);
create index for (h:HABITATGE) on (h.Id_Llar);

// Creem la relació VIU entre les persones i l'habitatge
LOAD CSV WITH HEADERS FROM 'https://docs.google.com/spreadsheets/d/e/2PACX-1vRM4DPeqFmv7w6kLH5msNk6_Hdh1wuExRirgysZKO_Q70L21MKBkDISIyjvdm8shVixl5Tcw_5zCfdg/pub?output=csv' AS row
MERGE (p:PERSONA {Id: toInteger(row.IND)})
MERGE (h:HABITATGE {Municipi: row.Location, Id_Llar: toInteger(row.HOUSE_ID)})
MERGE (p)-[:VIU {Any: toInteger(row.Year)}]->(h);

// Creem la relació SAME_AS entre les diferents persones
LOAD CSV WITH HEADERS FROM 'https://docs.google.com/spreadsheets/d/e/2PACX-1vTgC8TBmdXhjUOPKJxyiZSpetPYjaRC34gmxHj6H2AWvXTGbg7MLKVdJnwuh5bIeer7WLUi0OigI6wc/pub?output=csv' AS row
MERGE (p:PERSONA {Id: toInteger(row.Id_A)})
MERGE (q:PERSONA {Id: toInteger(row.Id_B)})
MERGE (p)-[:SAME_AS]->(q);

// Creem la relació FAMILIA amb les seves propietats entre les diferents persones
LOAD CSV WITH HEADERS FROM 'https://docs.google.com/spreadsheets/d/e/2PACX-1vRVOoMAMoxHiGboTjCIHo2yT30CCWgVHgocGnVJxiCTgyurtmqCfAFahHajobVzwXFLwhqajz1fqA8d/pub?output=csv' AS row 
MERGE (p:PERSONA {Id: toInteger(row.ID_1)})
MERGE (q:PERSONA {Id: toInteger(row.ID_2)})
MERGE (p)-[rel:FAMILIA {Relacio: row.Relacio, Relacio_Harmonitzada: row.Relacio_Harmonitzada}]->(q);
